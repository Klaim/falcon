/*#
 * @file        qrmask.fal
 * @brief       Masking.
 * @begin       17-Sep-2010
 * @author      <a href="mailto:giuseppe.greco@agamura.com">Giuseppe Greco</a>
 *
 * @copyright   (C) 2010 href="http://www.falconpl.org">The Falcon Programming Language</a>
 * @legalnotice Released under FPLL 1.1 or GPL 2.0 at your choice.
 */

load zlib
import QrConfig from .qrconfig as QrConfig
import QrSpec from .qrspec as QrSpec
import QrSpecConst from .qrspec as QrSpecConst

enum QrMaskConst
    n1 = 3
    n2 = 3
    n3 = 40
    n4 = 10
end

class QrMask
    _runLen = nil

    _mask = [
        [innerfunc (x, y); return (x + y) && 1; end],
        [innerfunc (x, y); return y && 1; end],
        [innerfunc (x, y); return x % 3; end],
        [innerfunc (x, y); return (x + y) % 3; end],
        [innerfunc (x, y); return (int(y / 2) + int(x / 3)) && 1; end],
        [innerfunc (x, y); return ((x * y) && 1) + (x * y) % 3; end],
        [innerfunc (x, y); return (((x * y) && 1) + (x * y) % 3) && 1; end],
        [innerfunc (x, y); return (((x * y) % 3) + ((x + y) && 1)) && 1; end]
    ]

    init
        self._runLen = arrayBuffer(QrSpecConst.max_width + 1, 0)
    end

    function writeFormatInformation(width, frame, mask, errorCorrectionLevel)
        blacks = 0
        format = QrSpec.getFormatInfo(mask, errorCorrectionLevel)

        for i in [0:8]
            if format && 1
                blacks += 2
                v = 0x85
            else
                v = 0x84
            end

            frame[8][width - 1 - i] = chr(v)
            if i < 6
                frame[i][8] = chr(v)
            else
                frame[i + 1][8] = chr(v)
            end
            format >>= 1
        end

        for i in [0:7]
            if format && 1
                blacks += 2
                v = 0x85
            else
                v = 0x84
            end

            frame[width - 7 + i][8] = chr(v)
            if i == 0
                frame[8][7] = chr(v)
            else
                frame[8][6 - i] = chr(v)
            end
            format >>=  1
        end

        return blacks
    end

    function generateMaskNo(maskNo, width, frame)
        bitMask = arrayBuffer(width)

        for y in [0:width]
            col = bitMask[y] = arrayBuffer(width, 0)
            for x in [0:width]
                if frame[y][*x] && 0x80
                    col[x] = 0
                else
                    col[x] = self._mask[maskNo](x, y) == 0 ? 1 : 0
                end
            end
        end

        return bitMask
    end

    function makeMaskNo(maskNo, width, frame, mask, maskGenOnly)
        if maskGenOnly == nil; maskGenOnly = false; end

        b = 0
        bitMask = []
        fileName = QrConfig.cache_dir + "mask_" + maskNo + "/" +  "mask_" + width + "_" + maskNo + ".dat"

        if QrConfig.cacheable
            if fileType(fileName) != FileStat.NOTFOUND
                inputStream = InputStream(fileName)
                bitMask = deserialize(inputStream)
                inputStream.close()
            else
                bitMask = self.generateMaskNo(maskNo, width, frame, mask)
                if fileType(QrConfig.cache_dir + "mask_" + maskNo) == FileStat.NOTFOUND
                    dirMake(QrConfig.cache_dir + "mask_" + maskNo)
                end
                
                outputStream = OutputStream(fileName)
                serialize(bitMask, outputStream )
                outputStream.close()
            end
        else
            bitMask = self.generateMaskNo(maskNo, width, frame, mask)
        end

        if maskGenOnly; return; end

        mask = clone(frame)

        for y in [0:width]
            for x in [0:width]
                if bitMask[y][x] == 1
                   mask[y][x] = chr(frame[y][*x] ^^ bitMask[y][x])
                end
                b += mask[y][*x] && 1
            end
        end

        return b
    end

    function makeMask(width, frame, maskNo, errorCorrectionLevel)
        masked = arrayBuffer(width)
        for y in [0:width]; masked[y] = "\x0" * width; end

        self.makeMaskNo(maskNo, width, frame, $masked)
        self.writeFormatInformation(width, $masked, maskNo, errorCorrectionLevel)

        return masked
    end

    function calcN1N3(length)
        demerit = 0

        for i in [0:length]
            if self._runLen[i] >= 5
                demerit += (QrMaskConst.n1 + (self._runLen[i] - 5))
            end
            if i && 1
                if (i >= 3) and (i < (length - 2)) and (self._runLen[i] % 3 == 0)
                    fact = int(self._runLen[i] / 3)
                    if (self._runLen[i - 2] == fact) and \
                        (self._runLen[i - 1] == fact) and \
                        (self._runLen[i + 1] == fact) and \
                        (self._runLen[i + 2] == fact)
                        if (self._runLen[i - 3] < 0) or (self._runLen[i - 3] >= (4 * fact))
                            demerit += QrMaskConst.n3
                        elif ((i + 3) >= length) or (self._runLen[i + 3] >= (4 * fact))
                            demerit += QrMaskConst.n3
                        end
                    end
                end
            end
        end

        return demerit
    end

    function evaluateSymbol(width, frame)
        head = 0
        demerit = 0

        for y in [0:width]
            head = 0
            self._runLen[0] = 1

            frameY = frame[y]
            if y > 0; frameYM = frame[y - 1]; end

            for x in [0:width]
                if x > 0 and y > 0
                    b22 = frameY[*x] && frameY[*(x - 1)] && frameYM[*x] && frameYM[*(x - 1)]
                    w22 = frameY[*x] || frameY[*(x - 1)] || frameYM[*x] || frameYM[*(x - 1)]
                    if (b22 || (w22 ^^ 1)) && 1; demerit += QrMaskConst.n2; end
                end
                if x == 0 and (frameY[*x] && 1)
                    self._runLen[0] = -1
                    head = 1
                    self._runLen[head] = 1
                elif x > 0
                    if (frameY[*x] ^^ frameY[*(x - 1)]) && 1
                        head++
                        self._runLen[head] = 1
                    else
                        self._runLen[head]++
                    end
                end
            end

            demerit += self.calcN1N3(head + 1)
        end

        for x in [0:width]
            head = 0
            self._runLen[0] = 1

            for y in [0:width]   
                if y == 0 and (frame[y][*x] && 1)
                    self._runLen[0] = -1
                    head = 1
                    self._runLen[head] = 1
                elif y > 0
                    if (frame[y][*x] ^^ frame[y - 1][*x]) && 1
                        head++
                        self._runLen[head] = 1
                    else
                        self._runLen[head]++
                    end
                end
            end

            demerit += self.calcN1N3(head + 1)
        end

        return demerit
    end

    function mask(width, frame, errorCorrectionLevel)
        minDemerit = 2147483647 // to be replaced with constant
        bestMaskNum = 0
        bestMask = []
        checkedMasks = [0, 1, 2, 3, 4, 5, 6, 7]

        if QrConfig.find_from_random != 0
            howManuOut = 8 - (QrConfig.find_from_random % 9)
            for i in [0:howManuOut]
                remPos = random(0, len(checkedMasks) - 1)
                arrayRemove(checkedMasks, remPos)
            end
        end

        bestMask = frame

        for i in checkedMasks
            mask = arrayBuffer(width)
            for y in [0:width]; mask[y] = "\x0" * width; end

            demerit = 0
            blacks = 0
            blacks = self.makeMaskNo(i, width, frame, $mask)
            blacks += self.writeFormatInformation(width, $mask, i, errorCorrectionLevel)
            blacks  = int(100 * blacks / (width * width))
            demerit = int(int(abs(blacks - 50) / 5) * QrMaskConst.n4)
            demerit += self.evaluateSymbol(width, mask)

            if demerit < minDemerit
                minDemerit = demerit
                bestMask = mask
                bestMaskNum = i
            end
        end

        return bestMask
    end
end

export QrMask
