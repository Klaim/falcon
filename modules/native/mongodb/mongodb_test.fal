/*
    Test sample for Falcon MongoDB module
    Need a running server on localhost:27017
 */

import from mongo

_n = 0

function Assert( x, msg )
    ++_n
    if not x
        > @"*** TEST FAILURE ($(_n)) ***"
        exit( _n )
    else
        > msg
    end
end

db = mongo.MongoDB( "olelew00tw00t", 4321 )
inspect( db )

// Getting host & port
> "Host = " + db.host()
> "Port = " + db.port()

// connect failure
try
    db.connect()
catch mongo.MongoDBError
    > "Caught connection failure!"
end

// Setting host & port
db.host( "127.0.0.1" )
db.port( 27017 )
> "Host = " + db.host()
> "Port = " + db.port()

// connect success
db.connect()
Assert( db.isConnected(), "Connected!" )

// drop database
Assert( db.dropDatabase( "none" ), "Dropped database!" )

// authentication failure
Assert( not db.authenticate( "none", "shall", "pass" ), "None shall pass!" )

// add user
Assert( db.addUser( "none", "shall", "pass" ), "Added user none!" )

// authentication success
Assert( db.authenticate( "none", "shall", "pass" ), "None can pass!" )

// create an empty bson object
obj = mongo.BSON()
inspect( obj )

// append something
obj.append([
    "key1", 3,
    "key2", 1.1,
    "key3", true,
    "key4", nil,
    "key5", "hello"
    ])

// reset object
obj.reset()

// disconnect
db.disconnect()
Assert( not db.isConnected(), "Disconnected!" )

> "TEST OK"
