/*
   FALCON - Samples for the DynLib dynamic library

   FILE: testdialog.fal

   Test a file open box under MS-Windows (using comdlg32).
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: 2006-05-11 12:35

   -------------------------------------------------------------------
   (C) Copyright 2004: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

load dynlib

lib = DynLib( "c:/windows/system32/comdlg32" )
// let's use the 8-bit string version; we could do also with 16 bit, but requeres a couple of tricks
opendlg = lib.get( "int GetOpenFileNameA( void* data )" )

data = MemBuf( 100, 1 )   // approx sise of the OPENFILENAME structure
memSet( data, 0, 100 )         // zero all the data

fname = MemBuf(1024,1)  // a string to get the filename
fname[0] = 0

setStruct( data, 0, 4, 22*4 )  // SIZE: 88 bytes - needs to be precise
setStruct( data, 12, 4, 
      stringToPtr( "All Files\x0*.*\x0TextFiles\x0*.txt; *.cfg\x0\x0" ) ) // File mask
setStruct( data, 24, 4, 1 ) // first string selected
setStruct( data, 28, 4, 
   memBufToPtr( fname ) ) // place to store the filename   
setStruct( data, 32, 4, 1024 )  // size of the filename

setStruct( data, 52, 4, 0x8000 )  // Flags

// perform the call
if opendlg( data )
   limitMembuf( fname )   // transform in a zero terminated string
   > "Selected file: ", strFromMemBuf( fname )
else
   > "No file selected."
end
