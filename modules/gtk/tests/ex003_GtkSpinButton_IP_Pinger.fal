/*
    Falcon-GTK Example 003
    ======================

    Little application to 'ping' some IP address (unfinished!),
    demonstrating the use of a GtkSpinButton.

    (Credits to billykater.)
 */

load gtk

m = GtkMain( args )

class MyWindow from GtkWindow
    button = nil
    spinner = []

    function on_delete_event()
        m.quit()
        return false
    end

    function on_value_changed()
        ip = []
        for i in [0:4]
            ip += self.spinner[i].get_value()
        end
        self.button.set_label( "Ping " + ".".merge( ip ) )
    end

    init
        self.set_property( "title", "IP Pinger" )
        self.signal_delete_event().connect( self )
        box = GtkVBox()
        self.add( box )

        hb = GtkHBox()
        for i in [0:4]
            spinner = GtkSpinButton( GtkAdjustment( 0.0, 0.0, 255.0, 1.0, 1.0, 0.0 ), 1, 0 )
            spinner.signal_value_changed().connect( self )
            self.spinner += spinner
            hb.add( spinner )
        end

        box.add( hb )
        self.button = GtkButton( "Test" )
        box.add( self.button )
    end
end

w = MyWindow()

m.run( w )

