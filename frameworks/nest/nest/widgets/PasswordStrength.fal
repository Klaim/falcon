/*
   FALCON - The Falcon Programming Language

   Nest - Falcon web applcation engine

   FILE: PasswordStrength.fal

   AJAX widgeting subsystem -- A field checking strengt of a password
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Mon, 26 Sep 2011 11:55:58 +0200

   -------------------------------------------------------------------
   (C) Copyright 2011: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import from nest.widgets.widget in widget

class PasswordStrength( id, pwd_widget ) from widget.Widget( id )
   tag = "span"
   pwd_widget = pwd_widget
   content = ""

   desc_level0 = "Insecure";
   desc_level1 = "Very weak";
   desc_level2 = "Weak";
   desc_level3 = "Strong";
   desc_level4 = "Very strong";

   pswdMeterFunc = '
   function( value ) {
      var m = 0;
      // has 6 chars minimum
      if ( value.length >= 6 ) {
         m += 20;
         // has both lower and uppercase
         if ( value.match( /^.*(?=.*[a-z])(?=.*[A-Z]).*$/ ) ) {
            m += 40;
         }
         // has a figure
         if ( value.match( /^.*(?=.*\d).*$/ ) ) {
            m += 20;
         }
         // has a special char
         if ( value.match( /^.*(?=.*[_@#$%%^&+=:;-]).*$/ ) ) {
            m += 20;
         }
      }
      
      this.innerHTML = this.getDesc( m );
      this.strenght = m;
      Nest.message(this.id, "strength", m);
   }
   '

   jsMethods = [
      "checkStrength" => self.pswdMeterFunc,
      "strenght" => 0
   ]

   jsListeners = [
      pwd_widget => "function( srcwid, msg, value ) { this.checkStrength(value); }"
   ]

   init
      hidvalue = widget.Widget( "strength" )
      hidvalue.tag = "input"
      hidvalue.props = ["type"=>"hidden", "value"=>0]
      self.addChild( hidvalue )

      // TODO: Remove this when we have automatic parentship
      self.addClassInParentship( PasswordStrength ) 
   end

   function onRender()      
      self.jsMethods["getDesc"] = @'
      function( m ) {
         if( m < 20 ){ return "$(self.desc_level0)"; }
         if( m < 40 ){ return "$(self.desc_level1)"; }
         if( m < 60 ){ return "$(self.desc_level2)"; }
         if( m < 80 ){ return "$(self.desc_level3)"; }
         return "$(self.desc_level4)";
      }
      '
   end

   function renderContent()
      return self.content
   end
end

