/*
   FALCON - The Falcon Programming Language

   Nest - Falcon web applcation engine

   FILE: Link.fal

   AJAX widgeting subsystem -- Simple widget implementation of A tag
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sun, 02 Oct 2011 13:53:51 +0200

   -------------------------------------------------------------------
   (C) Copyright 2011: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import Widget from nest.widgets.widget as Widget

/*# Widget representing an hyperlink ('a' HTML tag).
   @param id The id of the link
   @param href The location where the link is pointing to.
   @param content A string or a widget that will be rendered as link target.
   @optparam target A "target" property to be set in the 'a' HTML element.

   If the @b href is prefixed with "!", then the target is intended to be a
   link to a Nest page.
*/
class Link( id, href, content, target ) from Widget( id )
   tag = "a"
   target = target
   href = href
   content = content

   init
      if content provides addChild
         self.addChild( content )
      end

      if href.startsWith( "!" )
         href = Nest.pageLink( href[1:] )
      end
      
      settings = ["href" => href]
      if target: settings["target" ] = target
      
      self.set( settings )

      // TODO: Remove this when we have automatic parentship
      self.addClassInParentship( Link )   
   end

   function renderContent()
      if not self.content provides render
         return self.content
      else
         return self.content.render()
      end
   end
   
end
