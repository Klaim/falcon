/*
   FALCON - The Falcon Programming Language

   Nest - Falcon web applcation engine

   FILE: Button.fal

   AJAX widgeting subsystem -- Basic button (not for form submits)
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sun, 02 Oct 2011 13:53:51 +0200

   -------------------------------------------------------------------
   (C) Copyright 2011: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import from nest.widgets.widget in widget

/*# Basic button (not for form submits).
   @param id The widget ID
   @param content The HTML content displayed in the button.
   @optparam type Optional type (default: 'button')
   @optparam onclick A JS routine to call on click
            (if not specified, will emit a Nest onclick message)

   This generates an HTML button. Normally, you should
   rely to Submit or Reset fields in forms, as they generate the same
   controlled values in form submits in different browsers, while
   button tag based submits can differ (some use the value attribute,
   some use the contents of the tag).

   To attach actions to this button, use the @a nest.widget.Widget.jsListeners
   method.
*/
class Button( id, content, type, onclick ) from widget.Widget( id )
   tag = "button"
   content = content
   isSelfClosing = false

   init
      if self.props == nil: self.props = [=>]
      self.props['name'] = nil   // will be filled at render
      self.props['type'] = type ? type : "button"
      self.props['onclick'] = onclick ? onclick : self.jsEmit( 'onclick', "null")

      // TODO: Remove this when we have automatic parentship
      self.addClassInParentship( Button )
   end

   function renderContent()
      return self.content ? self.content : ""
   end

   /*# Turns this button in an AJAX form submitting button.
      @param w the form widget that is to be sent.
      @optparam confirmFunc A javascript function used to check if the form should be really sent.
   */
   function makeSendFormAJAX( w, confirmFunc )
      id = w.getFullID()
      xcall = @"Nest.i('$(id)').sendFormAJAX()"
      if confirmFunc
         xcall = "if(" + confirmFunc + "){" + xcall + ";}"
      end
      
      self.props['onclick'] = xcall
      return self
   end
end

