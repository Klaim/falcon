/*
   FALCON - The Falcon Programming Language

   Nest - Falcon web applcation engine

   FILE: plugins.fal

   Standard plugins for the Falcon Nest Wiki service
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Tue, 31 Aug 2010 23:06:38 +0200

   -------------------------------------------------------------------
   (C) Copyright 2010: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import from parser.genparser.node in gp


function _toc( param, node, sout, page )
   // we work only on the standout phase
   if not sout: return ""
   
   // traverse the nodes and find all the headers below "node"

   // The parameter, if given, is the depth of the page.
   if param
      try
         depth = int(param)
      catch
         depth = 1
      end
   else
      depth = 1
   end

   // ok, start parsing from here.
   resNode = nil
   stack = List()

   curlev = depth
   // headers are always toplevel
   n1 = node
   while n1
   
      if n1.type == "header"
         // first time around?
         if not resNode
            topNode = gp.Node( "ol" )
            resNode = gp.TagNode( "div", [ "class"=>"nest_wiki_toc" ] )
            resNode.add( topNode )
         end
         
         if n1.level >= depth
            while curlev < n1.level
               subNode = gp.Node( "ol" )
               topNode.add( subNode )
               stack.push( subNode )
               topNode = subNode
               ++curlev
            end

            while curlev > n1.level
               topNode = stack.pop()
               --curlev
            end

            nli = gp.Node( "li" )
            nli.add( gp.InfoNode("link", ["name"=>n1.content, "ext"=>false, "internal"=>true, "text"=>n1.content]))
            topNode.add( nli )
         end
      end

      // climb the hierarcy
      if n1.next
         n1 = n1.next
      else
         n1 = n1.parent
      end

      // we don't need to descend, because headers are all top level.
   end

   // all worth?
   if resNode: node.change( resNode )
end


function _timestamp( param, node, sout, page )
   if sout: return

   if param: return CurrentTime().toString(param)
   return CurrentTime().toString()
end

//====================================================

plugins = [
   "toc" => _toc,
   "timestamp" => _timestamp
]
