/*
   FALCON - The Falcon Programming Language

   Nest - Falcon web applcation engine

   FILE: readdir/main.fal

   Service used to show directory entries.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sun, 27 Jun 2010 14:01:38 +0200

   -------------------------------------------------------------------
   (C) Copyright 2010: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import from nest
import from nest.bindings in b


/*# Service receiving the variables in a form.

   This service is a useful shortcut and organizer for form checking code.

   It must be instantiated with a non-null intance. When instantiated, it moves
   all the variables named "<instance>.xxx" in self.formVars. Then, it launches
   the relative filters, which may alter them by returning a non-nil value
   (will be transformed to a string).
   
   For meta-information about the form processing, the filters may use the "formResult"
   hook.
*/
class Form( instance, options ) from nest.Service( instance, options )
   //# Functions accepting and checking the variables in the form.
   formFilters = nest.ServiceVar( b.ConfigBinder )
   //# Global check on the form; a function called as check( self, vars). Return goes in formResult.
   check = nest.ServiceVar( b.ConfigBinder )
   //# Action for the form submit
   action = nest.ServiceVar( b.ConfigBinder )
   //# Data to be displayed in the form.
   formData = nest.ServiceVar( b.ConfigBinder )
   //# Things to do below the form. For now, verbatim
   formActions = nest.ServiceVar( b.ConfigBinder )
   //# DOM ID of the form
   formId = nest.ServiceVar( b.ConfigBinder )
   //# CSS Class of the form
   formClass = nest.ServiceVar( b.ConfigBinder )
   //# Variables in the form as posted by the users
   formVars = nest.ServiceVar()
   //# Results that the final user may be interested in
   formResults = nest.ServiceVar()

   function startUp()
      if not self.instance
         raise nest.NestError( nest.NestError.other, "Service \"FormChecker\" must be given an instance name" )
      end
   end

   function run()
      Nest.logi( "Entering the " + self.fullName() + " service" )

      // check actions
      if self.action.value == nil
         self.action.value = Nest.pageLink()
      end
      
      // extract the variables
      formVars = [=>]
      for k, v in Nest.inputVars
         if k.startsWith( self.instance + "." )
            if Nest.log_level >= 3: Nest.logd( @"Adding form variable \"$k\"=\"$v\"" )
            formVars[ k[self.instance.len()+1:] ] = v
         end
      end

      // something to do?
      if formVars
         self.formVars.value = formVars
         for funcName, func in self.formFilters.value
            if funcName in formVars
               if Nest.log_level >= 3: Nest.logd( @"Calling form filter for \"$funcName\"" )
               res = func( self, funcName, formVars[funcName] )
               if Nest.log_level >= 3: Nest.logd( @"Result for filter \"$funcName\": $res" )
               if res: formVars[funcName] = res
            end
         end

         check = self.check.value
         if check.isCallable()
            check = check.base()
            Nest.logi( "Calling overall form check function" )
            res = check( self, bless(formVars) )
            if Nest.log_level >= 3: Nest.logd( @"Overall check function result: $res" )
            self.formResults.value = res
         end
      end
         
   end

   function __get_results()
      if self.formVars.isSet
         return self.formResults.value
      end
   end

    function __get_vars()
      return self.formVars.value
   end

   /*# Creates a field given a definition */
   function makeField( field )
      fvals = self.formVars.value
      value = field.name in fvals ? fvals[field.name] : ""
      
      return field.render( self.instance, value )
   end
end


class Field( name, desc, type, extra, opts, defValue )
   name = name
   desc = desc
   type = type
   extra = extra ? extra : ""
   opts = opts
   dvalue = defValue

   function render( instance, value )
      fname = instance + "." + self.name
      res = ""
      
      if self.desc
         desc = htmlEscape(self.desc)
         res += @'
            <label id="label_$(self.name)" for="$(self.name)">$(desc)</label>
            '
      end

      if not value and self.dvalue: value = strEscape( self.dvalue.toString() )
      
      // select is treated specially.
      if self.type == "select"
         res += @'<select name="$(fname)" id="$(self.name)" $(self.extra)>'
         
         for opt in self.opts
            res += "\n" + opt.render( value )
         end

         res += "\n</select>\n"
      else
         // otherwise, things are simpler.
         if self.type == "password"
            value = ""
         else
            value = strEscape( value )
         end
         
         res += @'
            <input type="$(self.type)" name="$(fname)" id="$(self.name)" value="$(value)" $(self.extra)/>
         '
      end
      
      return res
   end
   
end


class MultiField( name, desc, fields )
   name = name
   desc = desc
   fields = fields

   function render( instance, value )
      res = ""
      
      if self.desc
         desc = htmlEscape(self.desc)
         res += @'
            <label id="label_$(self.name)" for="$(self.name)">$(desc)</label>
            '
      end

      for field in self.fields
         res += field.render( instance, value )
      end

      return res
   end
end


class Option( value, desc )
   value = value
   desc = desc

   function render( val )
      esc_val = self.value.escape()
      desc = htmlEscape( self.desc )
      sel = val == self.value ? ' selected="yes"' : ""
      
      return @ "<option value=\"$(esc_val)\"$(sel)>$(desc)</option>\n"
   end
   
end
