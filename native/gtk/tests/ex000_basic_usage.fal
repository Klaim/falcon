/*
    Falcon-Gtk Example 000
    ======================

    This is a very simple application demonstrating the basic usage of
  Falcon-Gtk.

 */

// Load the module.
load gtk

/*
    Initialize gtk by creating a 'main' object.
    This is mandatory for ALL gtk applications to run correctly!
 */
m = GtkMain( args )

/*
    Create a custom window by subclassing GtkWindow.
 */
class MyWindow from GtkWindow
    // a delete callback to quit the main loop.
    function on_delete_event()
        m.quit()
        return false
    end
    // a button-clicked callback that prints something.
    function on_clicked()
        > "Yay!"
    end
    // a constructor.
    init
        // set window title with 'set_title' method...
        self.set_title( "Falcon" )
        // ... or set window title using a generic property setter.
        //self.set_property( "title", "Falcon" );
        // connect 'delete-event' to the class 'on_delete_event' method.
        self.signal_delete_event().connect( self )
        // make a vertical box
        vbox = GtkVBox()
        self.add( vbox )
        // add a label
        lbl = GtkLabel( "Hello World" )
        vbox.add( lbl )
        // add a button
        btn = GtkButton.new_with_label( "Falcon!" )
        // connect the button-clicked signal to the class 'on_clicked' method.
        btn.signal_clicked().connect( self )
        vbox.add( btn )
    end
end

// Initialize the window.
w = MyWindow()
// Show the window and run the main loop.
m.run( w )

