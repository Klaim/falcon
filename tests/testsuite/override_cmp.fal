/****************************************************************************
* Falcon test suite
*
*
* ID: 28b
* Category: classes
* Subcategory: override
* Short: Compare override
* Description:
*    Tests comparation overrides on classes.
* [/Description]
*
****************************************************************************/

globalCompare = compareByNum

class Over( name )
   name = name
   numval = 0
   array = []

   function compare( addend )
      return globalCompare( self, addend )
   end
end


function compareByNum( o1, o2 )
   if o2 provides numval
      return o1.numval - o2.numval
   elif o2.typeId() == NumericType
      return o1.numval - o2
   end
   return nil  // let the VM to order us.
end

function compareByName( o1, o2 )
   if o2 provides name
      return o1.name.compare( o2.name )
   elif o2.typeId() == StringType
      return o1.name.compare( o2 )
   end
   return nil  // let the VM to order us.
end

globalCompare = compareByNum

// compare on numbers
o = Over( "zero" )
if o > 1: failure( "o > 1" )
if not o < 1: failure( "o < 1" )
if o >= 1: failure( "o >= 1" )
if not o <= 1: failure( "o <= 1" )
if o == 1: failure( "o == 1" )
if not o != 1: failure( "o != 1" )

if o > 0: failure( "o > 0" )
if o < 0: failure( "o < 0" )
if not o >= 0: failure( "o >= 0" )
if not o <= 0: failure( "o <= 0" )
if not o == 0: failure( "o == 0" )
if o != 0: failure( "o != 0" )

o1 = Over( "one" )
o1.numval = 1

if o > o1: failure( "o > o1" )
if not o < o1: failure( "o < o1" )
if o >= o1: failure( "o >= o1" )
if not o <= o1: failure( "o <= o1" )
if o == o1: failure( "o == o1" )
if not o != o1: failure( "o != o1" )

// Check for VM ordering in dicts
// names and creation mixed to force randomic order in VM
o_five = Over( "five" )
o_five.numval = 5
o_two = Over( "two" )
o_two.numval = 2
o_four = Over( "four" )
o_four.numval = 4
o_three = Over( "three" )
o_three.numval = 3

dict = [ o1 => 1, o => 0, o_five => 5, o_four => 4, o_three => 3, o_two => 2 ]
res = []
for k,v in dict
   res += k
end

if res[0] != o or \
   res[1] != o1 or \
   res[2] != o_two or \
   res[3] != o_three or \
   res[4] != o_four or \
   res[5] != o_five
      failure( "Numeric ordering" )
end

// Now by name
globalCompare = compareByName
dict = [ o1 => 1, o => 0, o_five => 5, o_four => 4, o_three => 3, o_two => 2 ]

res = []
for k,v in dict
   res += k
end
if res[0] != o_five or \
   res[1] != o_four or \
   res[2] != o1 or \
   res[3] != o_three or \
   res[4] != o_two or \
   res[5] != o
      failure( "Name ordering" )
end

success()
