/****************************************************************************
* Falcon test suite
*
*
* ID: 109b
* Category: rtl
* Subcategory: file
* Short: Space-saving file operations
* Description:
*   Test on common files operations.
*   This test is analgue to 109a, but it uses the space-saving vesrion if the
*   file functions, that can reuse string buffers.
* [/Description]
*
****************************************************************************/

const filename = "109b.test"
const bufsize = 128
buffer = ""

try
   file = IOStream( filename, 0644, FILE_SHARE );
catch in error
   failure( "File creation: " + error.toString() )
end


try
   if file.tell() != 0: failure( "Tell at 0" )
   if file.write( "Hello world" ) != 11: failure( "Writing" )
   if file.tell() != 11: failure( "Tell at end" )
   // write a bit more, needed for readLine
   file.write( "\nNext line 1\r\nNext line 2\nNext line 3" );
   fileLen = file.tell()
   file.seek( 0 )
   if file.tell() != 0: failure( "Tell after seek" )
   if file.read( $buffer, 11 ) != 11 or buffer != "Hello world": failure( "Read back" )
   file.readLine( $buffer, bufsize ) // discards the first \n

   if file.readLine( $buffer, bufsize ) != len( "Next line 1" ): failure( "readline 1" )
   if buffer != "Next line 1": failure( "readline 2" )
   if file.readLine( $buffer, bufsize ) != len( "Next line 2" ): failure( "readline 2" )
   if buffer != "Next line 2": failure( "readline 2" )
   if file.readLine( $buffer, bufsize ) != len( "Next line 3" ): failure( "readline 3" )
   if buffer != "Next line 3": failure( "readline 3" )

   if not file.eof(): failure( "Should be at eof" )

   file.seek( 0 )
   if file.eof(): failure( "Eof reset" )
   file.seekEnd( 0 )
   if file.tell() != fileLen: failure( "Return to end position" )

   if file.eof(): failure( "early eof" )
   if file.read( $buffer, bufsize ) != 0: failure( "read at end of file" )
   if buffer != "": failure( "Buffer not reset" )
   if not file.eof(): failure( "Eof not detected." )

   // seekCur test.
   file.seek( 11 )
   file.seekCur( -5 )
   if file.read( $buffer, 5 ) != 5 or buffer != "world": failure( "seekCur" )
   file.close()

catch in error
   if error.fsError
      failure( "File operations: " + error.toString() + "\n" + systemErrorDescription(error.fsError) )
   else
      failure( "File operations: " + error.toString() )
   end
end

try
   fileRemove( filename )
catch in error
   failure( "File removal: " + error.toString() )
end

success()

/* End of file */
