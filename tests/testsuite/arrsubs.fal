/****************************************************************************
* Falcon test suite
*
*
* ID: 11j
* Category: types
* Subcategory: array
* Short: Ranged subscripts.
* Description:
* Checks subarrays and other complex operations.
* [/Description]
*
****************************************************************************/

array =  [ 0, 1, 2,
           3, 4, 5,
           6, 7
         ]

// get an empty range
a1  = array[0:0]
if len( a1 ) != 0: failure( "Get Empty range" )

// Get whole open range.
a1  = array[0:]
if len( a1 ) != 8: failure( "Get Full range - len" )
for i in [0:len(a1)]
    if a1[i] != array[i]: failure( "Get Full range - item " + i )
end

// direct range -- 1 item
a1  = array[2:3]
if len( a1 ) != 1: failure( "Get 1 item - len" )
if a1[0] != 2: failure( "Get 1 item - value" )

// direct range -- more items
a1  = array[2:5]
if len( a1 ) != 3: failure( "Get more elements - len" )
if a1[0] != 2 or a1[-1] != 4: failure( "Get more elements - value" )

// Reverse range -- full
a1  = array[-1:0]
if len( a1 ) != 8: failure( "Reverse range full - len" )
for i in [0:len(a1)]
    if a1[i] != array[len(a1)-i-1]: failure( "Reverse range full - item " + i )
end

// Reverse range -- partial
a1  = array[5:4]
if len( a1 ) != 2: failure( "Reverse range - len" )
if a1[0] != array[5] or a1[1] != array[4]: failure( "Reverse range - items" )

// Direct range with negative index
a1  = array[-5:-4]
if len( a1 ) != 1: failure( "Dirct range neg - len(1)" )
if a1[0] != array[-5]: failure( "Dirct range - items(1)" )

// Direct range with negative index
a1  = array[-5:-3]
if len( a1 ) != 2: failure( "Dirct range neg - len(2)" )
if a1[0] != array[-5] or a1[1] != array[-4]: failure( "Dirct range - items(2)" )

// Reverse range with negative index
a1  = array[-3:-5]
if len( a1 ) != 3: failure( "Reverse range neg - len(3)" )
if a1[0] != array[-3] or a1[2] != array[-5]: failure( "Dirct range - items(3)" )

// Reverse range open
a1  = array[-5:]
if len( a1 ) != 5: failure( "Reverse range open - len" )
if a1[4] != 7 or a1[0] != 3: failure( "Reverse range open - items" )

// Reverse range open -- empty
a1  = array[-1:]
if len( a1 ) != 1: failure( "Reverse range open last" )
if a1[0] != 7: failure( "Reverse range open last - item" )

success()

/* End of file */
