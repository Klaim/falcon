/*
   FALCON PROGRAMMING LANGUAGE

   CURL library binding - Samples

   send.fal - Shows how to send a file to a remote server

   USAGE: send.fal [-u <usernam>] [-p <password>] [-?] <URL> <file>
*/

import from curl

try
   CmdParser.parse()
   if CmdParser.url == nil or CmdParser.file == nil
      CmdParser.usage()
      exit(0)
   end
   
catch StringType
   exit(1)
end

try
   is = InputStream( CmdParser.file )
   h = curl.Handle( URI.encode(CmdParser.url) )
   // we want to receive the data as a stream.
   h.setInStream( is )
   h.setOption( curl.CURLOPT.UPLOAD, true )

   if CmdParser.user: h.setOption( curl.CURLOPT.USERNAME, CmdParser.user )
   if CmdParser.password: h.setOption( curl.CURLOPT.PASSWORD, CmdParser.password )

   > "Starting transfer; reading file ", args[1]
   h.exec()
   > "Transfer complete."

   is.close()
   h.cleanup()

   return 0
catch curl.CurlError in e
   > "ERROR!"
   > "Received the follwing error:"
   > e
   return 1
catch IoError in e
   > "Error in creating the output file: "
   > e
   return 1
end


 object CmdParser from CmdlineParser

   url = nil
   file = nil

   user = nil
   password = nil
   
   function onOption( option )
      switch option
         case "?", "help"
            self.usage()
         case "u", "p", "l", "long"
            self.expectValue()
         default
            self.unrecognized( option )
      end
   end

   function onValue( option, value )
      switch option
         case "p"
            self.password = value
         case "u"
            self.user = value
      end
   end

   function onFree( param )
      if self.url == nil
         self.url = param
      elif self.file == nil
         self.file = param
      else
         self.usage()
         raise "broken"
      end
   end

   function onSwitchOff( sw )
      switch sw
         case "k"
            // set switch k OFF
         case "n"
            // set switch n OFF
         default
            self.unrecognized( sw )
      end
   end

   function unrecognized( option )
      > "Unrecognized option: ", option
      self.usage()
      raise "broken"
   end

   function usage()
      > "USAGE: send.fal  [-u <usernam>] [-p <password>] [-?] <URL> <file>"
      >
   end
end
