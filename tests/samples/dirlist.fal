/*
   Falcon Samples.

   Directory listing.

   This script demonstrates the usage of the basic directory
   API.

   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Tue, 25 Dec 2007 13:36:39 +0100

   -------------------------------------------------------------------
   (C) Copyright 2007: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

//==================================================
// Print the current directory

printl( "Current directory: ", dirCurrent() )
dir = len( args ) == 0 ? "." : args[0]
printl( "Directory list for: ", dir )

try
   dirHandle = DirectoryOpen( dir )
   elem = dirHandle.read()
   stats = FileStat()
   while elem != nil
      fname = dir + "/" + elem
      if stats.readStats( fname )
         print( elem, "(", stats.type, ")", ":", stats.size, " ",  stats.mtime.toString() )
         if stats.type = FILE_TYPE_LINK
            printl( " ---> ", dirReadLink( fname )  )
         else
            printl()
         end
      else
         printl( "Failure getting stats for: ", dir + "/" + elem  )
      end
      elem = dirHandle.read()
   end
   dirHandle.close()

catch in error
   > "Error: =============  "
   > error
end


printl()
printl( "Done" )
