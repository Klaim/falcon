#!/usr/bin/falcon
/*
   FALCON - SDL MIXER MODULE Samples

   FILE: sdlmix_play.fal

   Generic music file support testing.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Thu, 09 Oct 2008 23:19:59 +0200
   Last modified because:

   -------------------------------------------------------------------
   (C) Copyright 2008: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

load sdlmixer


object listener
   init
      subscribe( "sdl_Quit", self )
   end
   
   function on_sdl_Quit()
      > "Quitting"
      exit(0)
   end
end


try
   a = SDL.InitAuto( SDL.INIT_AUDIO || SDL.INIT_VIDEO )

   MIX.OpenAudio( 44100, AUDIO.S16, 2, 4096 )
   music = MIX.LoadMUS( "fading.ogg" )

   > "Success. Press enter to stop"

   > "Fading in in 6 seconds"
   music.Play( fadeIn| 6 )
   count = 0
   stdin = stdIn()
   SDL.StartEvents()
   
   loop
      count++
      >> @"[$(count)]\r"
      if count == 80
         > "Pausing for two seconds:"
         MIX.PauseMusic()
      end

      if count == 90
         > "Checing if music is paused (", \
            MIX.PausedMusic(), ") and playing (", \
            MIX.PlayingMusic(), ")"
      end

      if count == 100
         > "Resuming music"
         MIX.ResumeMusic()
      end

      if count == 140
         > "Fading out in 5 secs "
         MIX.FadeOutMusic( 5 )
      end

      if count == 170
         > "... but killing it now"
         MIX.HaltMusic()
      end

   end stdin.readAvailable(0.1)

   MIX.CloseAudio()
catch in e
   > "Test failed: "
   > e
end

/* end of file */
