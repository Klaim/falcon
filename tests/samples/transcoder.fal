/*
   FALCON - Samples

   FILE: transcoder.fal

   $Id: transcoder.fal,v 1.2 2007/03/04 17:39:03 jonnymind Exp $

   Transcodes an input file from a certain encoding to another.
   This code is currently highly experimental.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin:
   Last modified because:

   -------------------------------------------------------------------
   (C) Copyright 2006: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
   In order to use this file in its compiled form, this source or
   part of it you have to read, understand and accept the conditions
   that are stated in the LICENSE file that comes boundled with this
   package.
*/

if len( args ) != 4
	printl( "usage: ", scriptName, " <infile> <inenc>  <outfile> <outenc>" )
	printl( "Knwon encodings: "
				"\tutf-8\t\tUnicode UTF-8\n"
				"\tutf-16\t\tUnicode UTF-16 (endian detecting)\n"
				"\tutf-16BE\tUnicode UTF-16 Big Endian\n"
				"\tutf-16LE\tUnicode UTF-16 Little Endian\n"
				"\tiso8859-<x>\twhere <x> 1..15: ISO tables\n"
				"\tcp1252\t\tMS-Windows code page LATIN\n"
				"\tC\t\tDirect untranslated byte encoding\n" )
   printl( "\n...Local system encoding: ", getSystemEncoding(), "\n" )
   return 0
end

try
	input = InputStream( args[0] )
	output = OutputStream( args[2] )
catch in error
	printl( "Error while opening streams: ", error.toString() )
	return 1
end

try
	input.setEncoding( args[1] )
catch
	printl( "Unknown encoding ", args[1] )
	return 1
end

try
	output.setEncoding( args[3] )
catch
	printl( "Unknown encoding ", args[3] )
	return 1
end

try
   buffer = ""
   while( input.readText( buffer, 1024 ) != 0 )
   	output.writeText( buffer )
   end

   input.close()
   output.close()
catch in error
	printl( "Error while transcoding streams: ", error.toString() )
	return 1
end

printl( "Operation complete." )
return 0

/* end of transcoder.fal */
