#!/usr/bin/falcon
/*
   FALCON - SDL MODULE Samples

   FILE: sdl_paint.fal

   Shows direct painting on SDL surfaces.
   Does the same things as paint2, but it uses a prebuilt membuf that
   is then delivered to a surface.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sun, 16 Mar 2008 21:15:18 +0100
   Last modified because:

   -------------------------------------------------------------------
   (C) Copyright 2008: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
   In order to use this file in its compiled form, this source or
   part of it you have to read, understand and accept the conditions
   that are stated in the LICENSE file that comes boundled with this
   package.
*/

load fsdl

try
   a = SDL.InitAuto( SDL.INIT_VIDEO )
   SDL.LoadBMP("falcon-ico.bmp").SetIcon()
   SDL.WM_SetCaption( "Falcon SDL paint test - 3" )
   screen = SDL.SetVideoMode( 640, 480 )
   // draw a cloured band
   format = screen.format
   bandPix = MemBuf( 640, format.BytesPerPixel )
   for bid in [0:640]
      bandPix[bid] = screen.MapRGBA( 150, bid%128, (bid+128)%256,  250 )
   end

   // try now to store our color band somewhere. - first hardware
   band = SDL.CreateRGBSurfaceFrom( bandPix, 640, 1, format.BitsPerPixel,
      format.Rmask, format.Gmask, format.Bmask, format.Amask )

   // then copy the result on the screen
   rect = SDLRect( 0, 0, 640, 1 )
   blitter = band.BlitSurface
   for y in [0:480]
      rect.y = y
      blitter( nil, screen, rect )
   end

   // make a solid rectangle
   r = SDLRect( 280, 200, 80, 80 )
   screen.FillRect( r,
         screen.MapRGBA( 255, 255, 255, 255 ) )

   screen.UpdateRect()

   > "Success! - press enter"
   input()
catch in e
   > "Test failed: "
   > e
end

/* end of file */
