#!/usr/bin/falcons
/*
   FALCON - Samples

   FILE: filecopymb.fal

   This script works as filecopy.fal, but it uses Memory Buffers (MemBuf)
   instead of strings to move files around.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: mer nov 3 2004

   -------------------------------------------------------------------
   (C) Copyright 2004: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/


if len( args ) != 2
   printl( "Usage: ", scriptName, " <source> <dest>" )
   exit(1)
end

printl( "Copying ", args[0], " into ", args[1] )

try
   // try opening the input stream.
   source = InputStream( args[0] )
   // then try opening the output stream
   dest = OutputStream( args[1] )

   // let's see how much we have to copy; a seek to end will do
   inSize = source.seekEnd(0)
   readSize = 0
   source.seek(0)

   // first, read a block initializing a suitable variable.
   block = MemBuf( 1024 )

   // continue until the last read returns 0 (and this sets the EOF flag).
   while not source.eof()
      block.rewind()
      count = source.read( block )
      // write the block. As memblocks are fixed length, we do NEED to
      // specify it
      dest.write( block, count )
      // because, as long as the block is a string, read will set its size correctly.
      readSize += count
      // a bit of bells and whistles
      gauge( inSize, readSize )
   end

   source.close()
   dest.close()

catch in err
   printl( "Can't complete the required operation." )
   printl( err.toString() )

   if err.systemError: printl( "System error. ", err.getSysErrorDesc() )
end

printl()
printl("Done.")

exit(0)

/**********************************
* A bit of a gauge
**********************************/
function gauge( max, current )
   static
      lastRatio = -1
   end

   if max == 0: return
   ratio = 60 * (current/max)
   if lastRatio == int(ratio)
      return
   else
      lastRatio = int(ratio)
   end

	gauge = strReplicate( "#", ratio )
	prc = current/max*100
   print(@"[$(gauge:60)] $(prc:6.2r)%\r")

end

/* end of filecopymb.fal */
