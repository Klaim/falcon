/****************************************************************************
* Falcon test suite -- DBI tests
*
*
* ID: 21d
* Category: mysql
* Subcategory:
* Short: MySQL query on procedure
* Description:
* Queries the recordset of a stored procedure. 
*  -- USES the table created by the first test and the data from test 20d
* [/Description]
*
****************************************************************************/

import from dbi
import mysql_conn_str from config

data = []
for i in [0:10]
   mb = MemBuf(10)
   for j = 0 to 9: mb[j] = (j+i+65)
   t = CurrentTime()
   t.second = i
   data += [[ 0, i+10, "fix char " + i, "varchar " + i,
         "Text blob " + i, mb,
         24589.21345/(i+1), t ]]
end

try
   conn = dbi.connect( config.mysql_conn_str )
   rs = conn.query( "call getData(?, ?)", 10, 20 )

   if rs.getColumnCount() != 8
      failure( "Can't get columns: " + rs.getColumnCount() )
   end

   if rs.getRowCount() != 10
      failure( "Can't get row count: " + rs.getRowCount() )
   end

   // fetch array
   arr = []
   count = 0
   while rs.fetch( arr )
      
      if arr[1] != data[count][1] or arr[2] != data[count][2] or \
            arr[3] != data[count][3] or arr[6] != data[count][6] or \
            arr[7].year != data[count][7].year
         failure( "Consistency check at step " + count )
      end
      ++count
   end

   if count != 10
      failure( "Fetched records " + count )
   end

   rs.close()
   // try to query again
   rs = conn.query( "select * from TestTable" )
   if rs.getColumnCount() == 0
      failure( "Can't re-seek the table" )
   end
   
   // fetch all
   count = 0
   while rs.fetch( arr )
      ++count
   end

   if count != rs.getRowCount()
      failure( "Problems fetching after call" )
   end
   
   conn.close()
   success()

catch dbi.DBIError in error
   failure( "Received a DBI error: " + error )
end
