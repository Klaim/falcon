/****************************************************************************
* Falcon test suite -- DBI tests
*
*
* ID: 20d
* Category: mysql
* Subcategory:
* Short: MySQL prepared statement
* Description:
*  Inserts multiple records using a prepared statement.
*  -- USES the table created by the first test
* [/Description]
*
****************************************************************************/

import from dbi
import mysql_conn_str from config

data = []
for i in [0:10]
   mb = MemBuf(10)
   for j = 0 to 9: mb[j] = (j+i+65)
   t = CurrentTime()
   t.second = i
   data += [[ i+10, "fix char " + i, "varchar " + i,
         "Text blob " + i, mb,
         24589.21345/(i+1), t ]]
end

try
   conn = dbi.connect( config.mysql_conn_str )

   stmt = conn.prepare("
      insert into TestTable( vint, fixchar, vchar, tblob, bblob, number, ts )
      values( ?, ?, ?, ?, ?, ?, ? )"
   )

   for elem in data
      (.[ stmt.execute ] + elem)()
   end

   stmt.close()
   conn.commit()
   conn.close()
   success()

catch dbi.DBIError in error
   failure( "Received a DBI error: " + error )
end
