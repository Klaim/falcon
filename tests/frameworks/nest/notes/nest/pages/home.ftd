<?
   import from nest.action in na
   import from nest.dbview in dw
   import DBView from nest.dbview as DBView

   notes = Nest.dm( "notes_db" ).entity( "notes" )

   tableview = DBView( notes )
   tableview.fields = .[
      dw.DBViewField( "inserted", i"Inserted on", nil, dw.DBOrder.DESCENDING )
      dw.DBViewField( "title", i"Title", {v=>"<span class='title'>" + htmlEscape(v) + "</span>"} )
      dw.DBViewField( "content", i"Content",
               { v=>
                     "<span class='content'>" +
                     ( v.len() > 30 ? v[0:27] + " ... " : v) +
                     "</span>"
               })
      ]
   
   login = Nest.service("Login")
   da = Nest.service( "DataControl", "",
         ["entity"=>notes]
         )
         
   dq = Nest.service( "DataTable", "",
      [
         "back_act" => .[
            na.ActionBox( na.LinkAction( Nest.AL.USER, "View", Nest.eidLink("view") ), "&nbsp;", "&nbsp;" )
            na.ActionBox( na.LinkAction( Nest.AL.STAFF, "Edit", Nest.eidLink("edit") ), "&nbsp;", "&nbsp;" )
         ],
         "front_act" => na.CheckboxAction( Nest.AL.STAFF ),
         "table_act" => .[
            na.SubmitAction( Nest.AL.STAFF, "Delete!", "cmd", "del" )
            na.ButtonAction( Nest.AL.STAFF, "Add new!", "window.location='" + Nest.pageLink("add")+"'" )
         ],
         "view" => tableview,
         "level" => Nest.AL.USER
      ] )
   
?><html>
<head>
   <title>Your notes</title>
   <?= Nest.headings() ?>
</head>
<body>
   <h1>Your notes</h1>
   <div id="login"><? login.render() ?></div>
   <p><? da.render() ?></p>
   <p>This is a simple test site showing how you could create an on-line notepad.</p>
   <p><?= na.Action(Nest.AL.STAFF, "Use this link to create a <a href=\"" + Nest.pageLink('add') +"\">new note</a>.").render() ?>
   <? dq.render() ?>
</body>
</html>
