<?
   import from nest.action in na
   import from nest.dbview in dw
   import DBView from nest.dbview as DBView

   notes = Nest.dm( "notes_db" ).entity( "notes" )

   tableview = DBView( notes )
   tableview.fields = .[
      dw.DBViewField( "inserted", i"Inserted on", nil, dw.DBOrder.DESCENDING )
      dw.DBViewField( "title", i"Title", {v=>"<span class='title'>" + htmlEscape(v) + "</span>"} )
      dw.DBViewField( "content", i"Content",
               { v=>
                     "<span class='content'>" +
                     ( v.len() > 30 ? v[0:27] + " ... " : v) +
                     "</span>"
               })
      ]

   cnfview = DBView( notes )
   cnfview.fields = .[
      dw.DBViewField( "inserted" )
      dw.DBViewField( "title", i"Title", {v=>"- " + htmlEscape(v) } )
   ]
   
   login = Nest.service("Login")
   da = Nest.service( "DataControl", "",
         [
          "entity"=>notes,
          "view" => cnfview,
          "level" => Nest.AL.STAFF]
         )
         
   dq = Nest.service( "DataTable", "",
      [
         "back_act" => .[
            na.ActionBox( na.LinkAction( "View", Nest.AL.USER, Nest.eidLink("view"), "View"  ), "&nbsp;", "&nbsp;" )
            na.ActionBox( na.LinkAction( "Edit", Nest.AL.STAFF, Nest.eidLink("edit"), "Edit" ), "&nbsp;", "&nbsp;" )
         ],

         "front_act" => .[
            na.CheckboxAction( "Delete?", Nest.AL.STAFF )
         ],
         
         "table_act" => .[
            na.SubmitAction( "Delete", Nest.AL.STAFF, "Delete!", "cmd", "del" )
            na.ButtonAction( "Add", Nest.AL.STAFF, "Add new!", "window.location='" + Nest.pageLink("add")+"'" )
         ],
         
         "view" => tableview,
         "handler" => Nest.pageLink("home", ["cnf"=>1]),
         "default_cnt" => 10,
         "level" => Nest.AL.USER
      ] )
   
?><html>
<head>
   <title>Your notes</title>
   <? Nest.headings() ?>
</head>
<body>
   <h1>Your notes</h1>
   <div id="login"><? login.render() ?></div>
   <p><? da.render() ?></p>
   <p>This is a simple test site showing how you could create an on-line notepad.</p>
   <p><?= na.TextAction( "Add one", Nest.AL.STAFF, "Use this link to create a <a href=\"" + Nest.pageLink('add') +"\">new note</a>.").render() ?>
   <? dq.render() ?>
</body>
</html>
