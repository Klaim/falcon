load gtk

m = GtkMain( args )

class MyWindow from GtkWindow
    button = nil
    spinner = []

    function on_delete_event()
        m.quit()
        return false
    end

    function on_value_changed()
        ip = []
        for i in [0:4]
            ip += self.spinner[i].get_value()
        end
        self.button.set_label( "Ping " + ".".merge( ip ) )
    end

    init
        self.set_property( "title", "IP Pinger" )
        self.signal_delete_event().subscribe( self )
        box = GtkVBox()
        self.add( box )

        hb = GtkHBox()
        for i in [0:4]
            spinner = GtkSpinButton( 0, 255, 1 )
            spinner.signal_value_changed().subscribe( self )
            self.spinner += spinner
            hb.add( spinner )
        end

        box.add( hb )
        self.button = GtkButton( "Test" )
        box.add( self.button )
    end
end

w = MyWindow()

m.run( w )

