/****************************************************************************
* Falcon test suite
*
*
* ID: 40g
* Category: rtl
* Subcategory: core
* Short: getProperty
* Description:
* The core function getProperty allows to access an object property
* by name.
* [/Description]
*
****************************************************************************/

class testClass
   prop1 = "value1"
   function func1()
      return "func1"
   end
end

object testObj
   prop2 = "value2"

   function testMethod()
      return "testMethod"
   end
end

cls = testClass()
array = []
array.prop1 = "orig value"
array.mth = function(); return "testMethod"; end
bd = bless([ "prop1" => "orig value", "mth" => {=>return "testMethod"} ] )

try
   prop1 = getProperty( cls, "prop1" )
   if prop1 != "value1": failure( "Get property on class" )
catch
   failure( "Property access on class" )
end

try
   method = getProperty( cls, "func1" )
   if not isCallable( method ): failure( "Method retrival on class" )
   value = method()
   if value != "func1": failure( "Method intdirect call on class" )
catch
   failure( "Method access on class" )
end

try
   prop2 = getProperty( testObj, "prop2" )
   if prop2 != "value2": failure( "Get property on object" )
catch
   failure( "Property access on object" )
end

try
   method2 = getProperty( testObj, "testMethod" )
   if not isCallable( method2 ): failure( "Method retrival on object" )
   value2 = method2()
   if value2 != "testMethod": failure( "Method intdirect call on object" )
catch
   failure( "Method access on object" )
end


//======================================================
try
   prop1 = getProperty( array, "prop1" )
   if prop1 != "orig value": failure( "Get property on array" )
catch
   failure( "Property access on array" )
end

try
   mth = getProperty( array, "mth" )
   if not isCallable( mth ): failure( "Method retrival on array" )
   value2 = mth()
   if value2 != "testMethod": failure( "Method intdirect call on array" )
catch
   failure( "Method access on array" )
end


//======================================================
try
   prop1 = getProperty( bd, "prop1" )
   if prop1 != "orig value": failure( "Get property on dict" )
catch
   failure( "Property access on dict" )
end

try
   mth = getProperty( bd, "mth" )
   if not isCallable( mth ): failure( "Method retrival on dict" )
   value2 = mth()
   if value2 != "testMethod": failure( "Method intdirect call on dict" )
catch
   failure( "Method access on dict" )
end

//======================================================
// negative tests.

try
   getProperty( cls, "not here!!" )
   failure( "Exception not raised for classes" )
catch in error
   if error.code != 33: failure( "Wrong exception raised for classes" )
end

try
   getProperty( testObj, "not here!!" )
   failure( "Exception not raised for object" )
catch in error
   if error.code != 33: failure( "Wrong exception raised for object" )
end

try
   getProperty( array, "not here!!" )
   failure( "Exception not raised for array" )
catch in error
   if error.code != 33: failure( "Wrong exception raised for array" )
end

try
   getProperty( bd, "not here!!" )
   failure( "Exception not raised for dict" )
catch in error
   if error.code != 33: failure( "Wrong exception raised for dict" )
end

success()

/* End of file */
