/*
   FALCON - Generic Parser

   FILE: creole_to_tree.fal

   Read and parses creole WIKI data, displaying the layout of the
   parsed context tree.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sat, 30 Aug 2010 09:42:22 +0200

   -------------------------------------------------------------------
   (C) Copyright 2008: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import from parser.genparser
import from parser.creole


function traverse( node, level )
   > "CTX"+level+": " + node.type + (node provides level ? " lvl: "+ node.level : "" )

   if node provides infos
      > node.infos.describe()
   end

   if node.content == nil or node.content.typeId() == StringType
      > node.content
   else
      for item in node.content
         traverse( item, level + 1 )
      end
   end
end


if args.len() == 0
   > "Please, indicate a file in creole wiki text format on the command line"
   return 1
end

is = InputStream(args[0])
// let's say it's utf-8...
is.setEncoding("utf-8")

ctx = parser.genparser.Context()
p = parser.creole.Parser()

p.initParser( "start" )
line = ""
while is.readLine( line, 120000 )
   p.parseLine( line, ctx )
end
is.close()

traverse( ctx.topNode, 0 )

